source: names is duckdb.table('usa_names.parquet') extend {
  rename: year_born is `year`
  measure: population is `number`.sum()
  dimension: decade is floor(year_born/10)*10
  measure: births_per_100k is floor(population/all(population)*100000)
  view: by_name is {
    group_by: name
    aggregate: population
    limit: 10
  }
  view: by_state is {
    group_by: state
    aggregate: births_per_100k
  }
  view: by_gender is {
    group_by: gender
    aggregate: population
  }
  view: by_decade is {
    group_by: decade
    aggregate: births_per_100k
    order_by: 1 asc
  }
}

// how big are the populations...
source: cohort is names->{
  group_by: gender, state, year_born
  aggregate: cohort_size is population 
} extend {
  measure: population is cohort_size.sum()
}

source: names2 is names extend {
  join_one: cohort on 
    gender = cohort.gender 
    and state = cohort.state 
    and year_born = cohort.year_born
  view: names_chart is {
    group_by: name
    aggregate: births_per_100k
    # line_chart
    nest: by_year is {
      group_by: year_born
      aggregate: population
    }
    # shape_mape 
    nest: by_state is {
      group_by: state
      aggregate: per_100k is population/exclude(population,name)*100000
    }
    limit: 10
  }
  view: iconic_names_by_state is {
    group_by: name, state, gender
    aggregate: 
      all_name is all(population, name)
      name_popularity is all(population, name) / all(population)
      state_popularity is population / all(population, state)
      // popularity is (population / all(population, state))/ ( population / all(population, state))
  } 
  -> {
    where: all_name > 3000 
    group_by: state
    nest: by_gender is {
      group_by: gender
      nest: name_list_detail is {
        group_by: 
          name, 
          popularity is state_popularity / name_popularity   
        limit: 20
        order_by: 2 desc
      }
    }
    order_by: 1
    limit: 20
  }

  # line_chart
  view: gender_year is {
    group_by: year_born
    aggregate: population
    group_by: gender is gender ? pick 'Female' when 'F' else 'Male'
  }

   # shape_map
   view: gender_by_state is {
      group_by: state
      aggregate: percent_female is population{where: gender='M'}/population
    }

  view: gender_neutral_names is {
    group_by: name
    aggregate: population
    nest: gender_year
    nest: gender_by_state
    having: population{where: gender='F'}/population ? > .10 & < .9
    limit: 10
  }

  # dashboard
  view: kelly_time_space_dashboard is {
    where: name = 'Kelly'
    group_by: name
    aggregate: population
    nest: gender_year
    nest: gender_by_state
    nest: by_decade is {
      group_by: decade
      nest: gender_by_state
    }
    nest: by_state is {
      group_by: state
      nest: gender_year
    }
  }

  // --
  // Name: Resurgent Names top 2 decades (step 2)
  // The first stage to the query finds the top two decades
  //  (see the previous example)
  // The second stage computes the difference between years
  //  and the total percentage in both decades (good for filtering)
  view:  resurgent_names is {
    group_by: name
    aggregate: population
    nest: by_decade is {
      group_by: decade
      aggregate: percent_in_decade is population/all(population)
      order_by: 2 desc
      limit: 2
    }
  }
  -> {
    where: population > 3000 
    group_by: 
      name
      population
    aggregate:
      years_apart is max(by_decade.decade)-min(by_decade.decade)
      total_percentage is by_decade.percent_in_decade.sum()
      max_percentage is max(by_decade.percent_in_decade)
    nest: by_decade is {
      select: by_decade.decade, by_decade.percent_in_decade
    }
    limit: 10
  }

  # dashboard
  view: name_dashboard is {
    where: name = 'Lloyd' | 'Anika' | 'Jessie' | 'Kelly' | 'Todd' | 'Chris'
    # line_chart
    nest: by_name is {
      group_by: decade
      aggregate: births_per_100k
      group_by: name
    }
    # bar_chart
    nest: by_name_view is {
      group_by: name
      aggregate: births_per_100k
      group_by: gender
    }
    # dashboard
    nest: name_dash is {
      group_by: name
      aggregate: population
      # line_chart
      nest: by_year is {
        group_by: year_born
        aggregate: population
      }
      # shape_map
      nest: by_state is {
        group_by: state
        aggregate: per_100k is population/cohort.population * 100000
      }
      peak_decade_state is {
        group_by: state, decade
        aggregate: per_100k is population/cohort.population * 100000
        limit: 5
      }
    }
  }

  view: search_index is {
    index: name, state by population
  }
  // these should move into the markdown file
}
>>>markdown
# Using nesting to better control charting.

I'm proposing that each of the metrics have auto generated time series.  With modification to our charting library, we should be a ble to reuse these timeseries charts with any dimensionality.

* by_dau_day
* by_dau_day_moving_avg_7
* by_dau_day_percent_of_first
* by_dau_week_over_week
* by_dau_month_over_month
* by_dau_year_over_year
>>>malloy
import {recalls} from "auto_recalls.malloy"
>>>markdown
### `by_year`
>>>malloy
# line_chart
run: recalls -> by_year
>>>markdown
###    `by_year_moving_avg`
>>>malloy
# line_chart
run: recalls -> by_year_moving_avg
>>>markdown
### dimensionalized smoothed using tabluar query
Using tablular query has both the date and values in the query.
>>>malloy
# line_chart
run: recalls -> {
    group_by: 
        # series
        Manufacturer
        # x
        recall_year 
    aggregate:
      # y
      recall_count
    order_by: recall_year
    limit: 100000

}
>>>markdown
### chart above smoothed

* notice

1) We are required to write the avg_moving function in the query, it isn't in the model
2) if we want to modify the the dimension (Manufacturer), we have to edit query in two places.
3) We can't really take advantage of modeling.
>>>malloy
# line_chart
run: recalls -> {
    group_by: 
        # series
        Manufacturer
        # x
        recall_year 
    calculate:
      # y
      recall_count_moving is avg_moving( recall_count, 5) {partition_by: Manufacturer order_by: recall_year}
    order_by: recall_year
    limit: 100000
}
>>>markdown
### Using Nesting...
1) we are able to use modeled components
>>>malloy
run: recalls -> {
    group_by: Manufacturer
    aggregate: recall_count
    # line_chart
    nest: by_year
    limit: 3
}
>>>markdown
### We can easily change dimensionality
>>>malloy
run: recalls -> {
    group_by: Component
    aggregate: recall_count
    # line_chart
    nest: by_year
}
>>>markdown
### We can easily smooth using modeled components
>>>malloy
run: recalls -> {
    group_by: Manufacturer
    aggregate: recall_count
    # line_chart
    nest: by_year_moving_avg
}
>>>markdown
## if the renderer were smarter, it could render this as a single visualization
The second stage simply re-aranges the data so our charting library can deal with it.  This could be coded into the library.
>>>malloy
# line_chart
run: recalls -> {
    group_by: Manufacturer
    aggregate: recall_count
    # line_chart
    nest: by_year_moving_avg
    limit: 3
} 
// re-arange the data so our charting library can deal with it.
-> {
    group_by: 
      # series
      Manufacturer
      # x
      by_year_moving_avg.recall_year
    # y
    aggregate: by_year_moving_avg.recall_count_moving_avg.sum()
    limit: 10000
    order_by: recall_year
}
>>>markdown
### Growth from first Value
A common pattern is to look at growth from some particular starting point.  Make it easy to see changes between dimensional values.
>>>malloy
# line_chart
run: recalls -> {
    where: recall_year >=2000
    group_by: Manufacturer
    aggregate: recall_count
    # line_chart
    nest: by_year_percent_of_first
    limit: 3
} 
// re-arange the data so our charting library can deal with it.
-> {
    group_by: 
      # series
      Manufacturer
      # x
      by_year_percent_of_first.recall_year
    # y
    aggregate: by_year_percent_of_first.recall_count_percent_of_first.sum()
    limit: 10000
    order_by: recall_year
}
>>>markdown
### Period over period
Period over period uses the same technique to build nested series easily and understandably.
>>>malloy
# line_chart
run: recalls -> {
    group_by: recall_decade

    # line_chart
    nest: by_year is {
      group_by: year_offset is recall_year - recall_decade
      aggregate: recall_count
      order_by: year_offset
    }
  }
  -> {
    group_by: 
      # series
      recall_decade
      # x
      by_year.year_offset
    # y
    aggregate: by_year.recall_count.sum()
    limit: 10000
    order_by: year_offset
}
>>>markdown
### Placed in the model, it is easy to sue.
>>>malloy
# line_chart
run: recalls -> decade_over_decade + {
    where: recall_year >= 1970
}
>>>markdown
### Filtering let's you create specfic graphs.
>>>malloy
# line_chart
run: recalls -> decade_over_decade + {
    where: Manufacturer ~'Ford%'
    where: recall_year >= 1970
}
>>>markdown
### Nesting lets you expand across 
>>>malloy
run: recalls -> {
    group_by: Manufacturer
    aggregate: recall_count
    # line_chart
    nest: decade_over_decade 
    where: recall_year >= 1970
    limit: 3
}
// Auto Recalls: CSV example
##! m4warnings
source: recalls is duckdb.table('auto_recalls.csv') extend {
  measure:
    recall_count is count()
    percent_of_recalls is recall_count/all(recall_count)*100
    total_defects is `Potentially Affected`.sum()

    # link
  dimension:
    recall_url is concat(
      'https://www.nhtsa.gov/recalls?nhtsaId=',
      `NHTSA ID`
    )
    recall_year is year(`Report Received Date`)
    recall_decade is floor(year(`Report Received Date`)/10)*10
  view: by_manufacturer is {
    group_by: `Manufacturer`
    aggregate:
      recall_count
      total_defects
      percent_of_recalls
  }

  view: by_type is {
    group_by: `Recall Type`
    aggregate:
      recall_count
      percent_of_recalls
  }

  view: by_year is {
    group_by: recall_year 
    aggregate:
      recall_count
    order_by: recall_year
  }

  view: by_year_moving_avg is {
    group_by: recall_year
    calculate:
      recall_count_moving_avg is avg_moving(recall_count, 5)
    order_by: recall_year
  }

  view: by_year_percent_of_first is {
    group_by: recall_year
    calculate:
      # percent
      recall_count_percent_of_first is recall_count/first_value(recall_count) {order_by: recall_year}
    order_by: recall_year
  }

 
  view: decade_over_decade is {
    group_by: recall_decade

    # line_chart
    nest: by_year is {
      group_by: year_offset is recall_year - recall_decade
      aggregate: recall_count
      order_by: year_offset
    }
  }
  -> {
    group_by: 
      # series
      recall_decade
      # x
      by_year.year_offset
    # y
    aggregate: by_year.recall_count.sum()
    limit: 10000
    order_by: year_offset
}


  # line_chart
  view: by_year_manufacturer_line_chart is {
    nest: top_manufacturers is {
      group_by: Manufacturer
      aggregate: recall_count
      limit: 5
    }
    nest: x is {
      group_by: Manufacturer, recall_year is year(`Report Received Date`)
      aggregate:
        recall_count
    }
  }
  -> {
    where: top_manufacturers.Manufacturer = x.Manufacturer
    # x
    group_by: x.recall_year
    # y
    group_by: x.recall_count
    # series
    group_by: x.Manufacturer
    limit: 100000
    order_by: 1, 3
  }

  view: recent_recalls is {
    group_by:
      recall_date is `Report Received Date`::string
      `NHTSA ID`
      recall_url
      Manufacturer
      Subject
      `Potentially Affected`
    order_by: 1 desc
    limit: 10
  }

  view: biggest_recalls is recent_recalls + {
    order_by: `Potentially Affected` desc
  }

  # dashboard
  view: recall_dashboard is by_manufacturer + {
    # line_chart
    nest: by_year_line_chart is by_year + {group_by: `Recall Type`}
    nest: by_type
    nest: recent_recalls + {limit: 6}
    nest: biggest_recalls + {limit: 6}
  }
}
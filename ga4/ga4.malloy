
source: events is duckdb.sql("""
  SELECT
    *,
    (event_timestamp / 1000)::bigint as event_timestamp_ms
  FROM 'ga4_events_sampled.snappy.parquet'
  """) extend {
  rename:
    event_timestamp_raw is event_timestamp

  dimension:
    event_timestamp is epoch_ms!timestamp(event_timestamp_ms)
    new_user is pick 1 when event_name ? 'first_visit' | 'first_open' else 0
    event_value is (event_params.value.int_value ?? event_params.value.float_value ?? event_params.value.double_value)

  measure:
    is_new_user is max(new_user)
    user_count is count(user_pseudo_id)
    session_count is count(event_params.value.int_value) { where: event_params.key = 'ga_session_id' }

  view: nested_index is {
    index: event_params.*, user_properties.*, items.*
  }
}

-- Example: Query a specific date range for selected events.
--
-- Counts unique events by date and by event name for a specifc period of days and
-- selected events(page_view, session_start, and purchase).

// SELECT
//   event_date,
//   event_name,
//   COUNT(*) AS event_count
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name IN ('page_view', 'session_start', 'purchase')
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201201' AND '20201202'
// GROUP BY 1, 2;

run: events -> {
  group_by:
    event_date
    event_name

  aggregate:
    event_count is count()

  where:
    (event_name ? 'page_view' | 'session_start' | 'purchase')
    and event_timestamp.day ? @2020-12-01 to @2020-12-03
}


-- Example: Get 'Total User' count and 'New User' count.

// WITH
//   UserInfo AS (
//     SELECT
//       user_pseudo_id,
//       MAX(IF(event_name IN ('first_visit', 'first_open'), 1, 0)) AS is_new_user
//     -- Replace table name.
//     FROM `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
//     -- Replace date range.
//     WHERE _TABLE_SUFFIX BETWEEN '20201101' AND '20201130'
//     GROUP BY 1
//   )
// SELECT
//   COUNT(*) AS user_count,
//   SUM(is_new_user) AS new_user_count
// FROM UserInfo;

run: events -> {
  group_by: user_pseudo_id
  aggregate: is_new_user
  where: event_timestamp.day ? @2020-11-01 to @2020-12-01
} -> {
  aggregate:
    user_count is count()
    new_user_count is sum(is_new_user)
}


-- Example: Average number of transactions per purchaser.

// SELECT
//   COUNT(*) / COUNT(DISTINCT user_pseudo_id) AS avg_transaction_per_purchaser
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name IN ('in_app_purchase', 'purchase')
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201201' AND '20201231';

run: events -> {
  aggregate:
    avg_transaction_per_purchaser is count() / user_count

  where:
    (event_name ? 'in_app_purchase' | 'purchase')
    and (event_timestamp.day ? @2020-12-01 to @2021-01-01)
}

-- Example: Query values for a specific event name.
--
-- Queries the individual timestamps and values for all 'purchase' events.

// SELECT
//   event_timestamp,
//   (
//     SELECT COALESCE(value.int_value, value.float_value, value.double_value)
//     FROM UNNEST(event_params)
//     WHERE key = 'value'
//   ) AS event_value
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name = 'purchase'
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201201' AND '20201202';

run: events -> {
  select:
    event_timestamp
    event_value

  where:
    event_name = 'purchase'
    and (event_timestamp.day ? @2020-12-01 to @2020-12-02)
    and event_params.key = 'value'

  order_by: event_timestamp
}

// -- Example: Query total value for a specific event name.
// --
// -- Queries the total event value for all 'purchase' events.

// SELECT
//   SUM(
//     (
//       SELECT COALESCE(value.int_value, value.float_value, value.double_value)
//       FROM UNNEST(event_params)
//       WHERE key = 'value'
//     ))
//     AS event_value
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name = 'purchase'
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201201' AND '20201202';

run: events -> {
  aggregate:
    total_event_value is event_value.sum()

  where:
    event_name = 'purchase'
    and (event_timestamp.day ? @2020-12-01 to @2020-12-03)
    and event_params.key = 'value'
}

-- Example: Top 10 items added to cart by most users.

// SELECT
//   item_id,
//   item_name,
//   COUNT(DISTINCT user_pseudo_id) AS user_count
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`, UNNEST(items)
// WHERE
//   -- Replace date range.
//   _TABLE_SUFFIX BETWEEN '20201101' AND '20210131'
//   AND event_name IN ('add_to_cart')
// GROUP BY
//   1, 2
// ORDER BY
//   user_count DESC
// LIMIT 10;

run: events -> {
  group_by:
    items.item_id
    items.item_name

  aggregate: 
    user_count

  where:
    event_name = 'add_to_cart'
    and (event_timestamp.day ? @2020-11-01 to @2021-11-01)

  order_by: user_count desc
  limit: 10
}

-- Example: Average number of pageviews by purchaser type.

// WITH
//   UserInfo AS (
//     SELECT
//       user_pseudo_id,
//       COUNTIF(event_name = 'page_view') AS page_view_count,
//       COUNTIF(event_name IN ('in_app_purchase', 'purchase')) AS purchase_event_count
//     -- Replace table name.
//     FROM `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
//     -- Replace date range.
//     WHERE _TABLE_SUFFIX BETWEEN '20201201' AND '20201202'
//     GROUP BY 1
//   )
// SELECT
//   (purchase_event_count > 0) AS purchaser,
//   COUNT(*) AS user_count,
//   SUM(page_view_count) AS total_page_views,
//   SUM(page_view_count) / COUNT(*) AS avg_page_views,
// FROM UserInfo
// GROUP BY 1;

run: events -> {
  group_by:
    user_pseudo_id

  aggregate:
    page_view_count is count() { where: event_name = 'page_view' }
    purchase_event_count is count() { where: event_name ? 'in_app_purchase' | 'purchase' }

  where:
    event_timestamp.day ? @2020-12-01 to @2020-12-03
} -> {
  group_by:
    purchaser is purchase_event_count > 0
  
  aggregate:
    user_count is count()
    total_page_views is sum(page_view_count)
    avg_page_views is sum(page_view_count) / count()
}

-- Example: Sequence of pageviews.

// SELECT
//   user_pseudo_id,
//   event_timestamp,
//   (SELECT value.int_value FROM UNNEST(event_params) WHERE key = 'ga_session_id') AS ga_session_id,
//   (SELECT value.string_value FROM UNNEST(event_params) WHERE key = 'page_location')
//     AS page_location,
//   (SELECT value.string_value FROM UNNEST(event_params) WHERE key = 'page_title') AS page_title
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name = 'page_view'
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201201' AND '20201202'
// ORDER BY
//   user_pseudo_id,
//   ga_session_id,
//   event_timestamp ASC;

run: events -> {
  group_by:
    user_pseudo_id
    event_timestamp

  aggregate:
    ga_session_id is max(event_params.value.int_value) { where: event_params.key = 'ga_session_id' }
    page_location is max(event_params.value.string_value) { where: event_params.key = 'page_location' }
    page_title is max(event_params.value.string_value) { where: event_params.key = 'page_title' }

  where:
    event_name = 'page_view'
    and event_timestamp.day ? @2020-12-01 to @2020-12-03

  order_by: user_pseudo_id, ga_session_id, event_timestamp asc
}

// Tried to do this with a `nest + project` but generated invalid SQL:
run: events -> {
  group_by:
    user_pseudo_id
    event_timestamp

  nest: ga_session is {
    select: ga_session_id is event_params.value.int_value
    where: event_params.key = 'ga_session_id'
  }

  where:
    event_name = 'page_view'
    and event_timestamp.day ? @2020-12-01 to @2020-12-03
}

-- Example: List all available event parameters and count their occurrences.

// SELECT
//   EP.key AS event_param_key,
//   COUNT(*) AS occurrences
// FROM
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`, UNNEST(event_params) AS EP
// WHERE
//   _TABLE_SUFFIX BETWEEN '20201201' AND '20201202'
// GROUP BY
//   event_param_key
// ORDER BY
//   event_param_key ASC;

run: events -> {
  group_by: event_params.key
  aggregate: occurences is count()
  where: event_timestamp.day ? @2020-12-01 to @2020-12-03
  order_by: key
}

// https://developers.google.com/analytics/bigquery/advanced-queries
-- Example: Products purchased by customers who purchased a specific product.
--
-- `Params` is used to hold the value of the selected product and is referenced
-- throughout the query.

// WITH
//   Params AS (
//     -- Replace with selected item_name or item_id.
//     SELECT 'Google Navy Speckled Tee' AS selected_product
//   ),
//   PurchaseEvents AS (
//     SELECT
//       user_pseudo_id,
//       items
//     FROM
//       -- Replace table name.
//       `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
//     WHERE
//       -- Replace date range.
//       _TABLE_SUFFIX BETWEEN '20201101' AND '20210131'
//       AND event_name = 'purchase'
//   ),
//   ProductABuyers AS (
//     SELECT DISTINCT
//       user_pseudo_id
//     FROM
//       Params,
//       PurchaseEvents,
//       UNNEST(items) AS items
//     WHERE
//       -- item.item_id can be used instead of items.item_name.
//       items.item_name = selected_product
//   )
// SELECT
//   items.item_name AS item_name,
//   SUM(items.quantity) AS item_quantity
// FROM
//   Params,
//   PurchaseEvents,
//   UNNEST(items) AS items
// WHERE
//   user_pseudo_id IN (SELECT user_pseudo_id FROM ProductABuyers)
//   -- item.item_id can be used instead of items.item_name
//   AND items.item_name != selected_product
// GROUP BY 1
// ORDER BY item_quantity DESC;

source: purchase_events is events extend { 
  where: event_name = 'purchase' 

  measure:
    _total_spend_double is event_params.value.double_value.sum() { where: event_params.key = 'value' }
    _total_spend_float is event_params.value.float_value.sum() { where: event_params.key = 'value' }
    _total_spend_int is event_params.value.int_value.sum() { where: event_params.key = 'value' }

    total_spend is _total_spend_double + _total_spend_float + _total_spend_int
    purchase_count is count()

  dimension:
    item_of_interest is 'Google Navy Speckled Tee'

  view: by_country is {
    group_by: geo.country
    aggregate:
      total_spend
      purchase_count
  }

  view: by_year is {
    group_by: the_year is event_timestamp.year
    aggregate:
      total_spend
      purchase_count
  }
}

run: purchase_events -> {

  extend: {
    join_one: purchasers is purchase_events -> {
      group_by: user_pseudo_id

      where:
        (event_timestamp.day ? @2020-11-01 to @2021-02-01)
        and items.item_name = item_of_interest
    } extend {} on user_pseudo_id = purchasers.user_pseudo_id

    dimension:
      is_purchaser is purchasers.user_pseudo_id != null
  }

  group_by: items.item_name
  aggregate: item_quantity is items.quantity.sum()

  where: 
    is_purchaser
    and items.item_name != item_of_interest
}

// Average amount of money spent per purchase session by user
// SELECT
//   user_pseudo_id,
//   COUNT(
//     DISTINCT(SELECT EP.value.int_value FROM UNNEST(event_params) AS EP WHERE key = 'ga_session_id'))
//     AS session_count,
//   AVG(
//     (
//       SELECT COALESCE(EP.value.int_value, EP.value.float_value, EP.value.double_value)
//       FROM UNNEST(event_params) AS EP
//       WHERE key = 'value'
//     )) AS avg_spend_per_session_by_user,
// FROM
//   -- Replace table name.
//   `bigquery-public-data.ga4_obfuscated_sample_ecommerce.events_*`
// WHERE
//   event_name = 'purchase'
//   -- Replace date range.
//   AND _TABLE_SUFFIX BETWEEN '20201101' AND '20210131'
// GROUP BY
//   1;

run: purchase_events -> {
  group_by:
    user_pseudo_id

  aggregate:
    session_count
    avg_spend_per_session is total_spend / session_count

  where:
    event_timestamp.day ? @2020-11-01 to @2021-02-01

  order_by: 1
}

// Other queries of interest

run: purchase_events -> {
  group_by: device.operating_system
  aggregate:
    total_spend
    pct_of_total is total_spend / all(total_spend)
}

run: purchase_events -> nested_index -> {
  select: *
  order_by: weight desc
}

run: purchase_events -> nested_index -> {
  select: *
  where: fieldName ~ '%key'
  order_by: weight desc
}

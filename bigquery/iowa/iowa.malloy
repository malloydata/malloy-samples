source: iowa is table('malloy-data.iowa_liquor_sales.sales_deduped'){

  rename: _vendor_name is vendor_name
  // dimensions
  dimension:
    gross_margin is 100 * (state_bottle_retail - state_bottle_cost) / nullif(state_bottle_retail, 0)
    price_per_100ml is state_bottle_retail / nullif(bottle_volume_ml, 0) * 100

    category_class is category_name ?
      pick 'WHISKIES' when ~ r'(WHISK|SCOTCH|BOURBON|RYE)'
      pick 'VODKAS' when ~ r'VODKA'
      pick 'RUMS' when ~ r'RUM'
      pick 'TEQUILAS' when ~ r'TEQUILA'
      pick 'LIQUEURS' when ~ r'(LIQUE|AMARETTO|TRIPLE SEC)'
      pick 'BRANDIES' when ~ r'BRAND(I|Y)'
      pick 'GINS' when ~ r'GIN'
      pick 'SCHNAPPS' when ~ r'SCHNAP'
      else 'OTHER'

    bottle_size is bottle_volume_ml ?
      pick 'jumbo (over 1000ml)' when > 1001
      pick 'liter-ish' when >= 750
      else 'small or mini (under 750ml)'

    vendor_name is upper(_vendor_name)
  // measures
  measure:
    # currency dollars
    total_sale_dollars is sale_dollars.sum()
    item_count is count(distinct item_number)
    total_bottles is bottles_sold.sum()
    line_item_count is count()
    avg_price_per_100ml is price_per_100ml.avg()
    percent_of_sales is total_sale_dollars / all(total_sale_dollars) * 100

  // queries
  # line_chart
  query: by_month is {
    order_by: 1
    group_by: purchase_month is `date`.week
    aggregate: total_sale_dollars
  }

  query: top_sellers_by_revenue is {
    top: 5
    group_by:
      vendor_name
      item_description
    aggregate:
      total_sale_dollars
      total_bottles
      avg_price_per_100ml
      percent_of_sales
  }

  query: most_expensive_products is {
    top: 10
    order_by: avg_price_per_100ml desc
    group_by:
      vendor_name
      item_description
    aggregate:
      total_sale_dollars
      total_bottles
      avg_price_per_100ml
  }

  # bar_chart
  query: by_vendor is {
    group_by: vendor_name
    aggregate:
      total_sale_dollars
      total_bottles
    calculate: rank is row_number()
  } -> {
    group_by: 
      vendor_name is 
        pick vendor_name when rank < 10
        else 'other'
      aggregate:
        total_sale_dollars is total_sale_dollars.sum()
        total_bottles is total_bottles.sum()
  }

  # bar_chart
  query: by_class is {
    top: 10
    group_by: category_class
    aggregate:
      total_sale_dollars
      item_count
  }

  # bar_chart
  query: by_category is {
    top: 10
    group_by: category_name
    aggregate:
      total_sale_dollars
      item_count
  }

  # bar_chart
  query: by_sku is {
    group_by: item_description
    aggregate: total_sale_dollars
    limit: 5
  }

  query: vendor_dashboard is {
    aggregate: vendor_count is count(distinct vendor_number)
    aggregate:
      total_sale_dollars
      total_bottles
    nest:
      by_month
      by_class
      by_vendor
      top_sellers_by_revenue
      most_expensive_products
      by_vendor_dashboard is {
        top: 10
        group_by: vendor_name
        aggregate: total_sale_dollars
        nest:
          by_month,
          top_sellers_by_revenue,
          most_expensive_products
     }
  }
}

query: vodka_dashboard is iowa->vendor_dashboard {
  where: category_class ? 'VODKAS'
}

query: tequila_dashboard is iowa->vendor_dashboard {
  where: category_class ? 'TEQUILAS'
}

query: luxco_dashboard is iowa->vendor_dashboard {
  where: vendor_name ? ~ r'Luxco'
}

# dashboard
query: iowa-> {
  nest: by_class
  nest: by_vendor
  nest: by_month
  nest: by_county is {
    group_by: county
    aggregate: total_sale_dollars
  }
}